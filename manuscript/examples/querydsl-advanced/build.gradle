/*
 * See comments in ../querydsl-basic/build.gradle. This build generates Querydsl but not JPA
 * metamodel, otherwise there are no other important differences.
 */
plugins {
  id 'java'
}

repositories {
  jcenter()
}

configurations {
  querydslApt
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

ext {
  querydslVersion = '4.1.4'
  hibernateVersion = '5.2.2.Final'
  eclipseLinkVersion = '2.6.2'
  h2Version = '1.4.190'
  antlrVersion = '4.5.3'
}

dependencies {
  compileOnly 'javax:javaee-api:7.0'
  compile "com.querydsl:querydsl-jpa:$querydslVersion"
  compile "org.hibernate:hibernate-entitymanager:$hibernateVersion"
  compile "org.eclipse.persistence:org.eclipse.persistence.jpa:$eclipseLinkVersion"
  compile "com.h2database:h2:$h2Version"
  // For experiment with expressions evaluator
  compile "org.antlr:antlr4-runtime:$antlrVersion"

  querydslApt "com.querydsl:querydsl-apt:$querydslVersion"
}

sourceSets {
  main {
    output.resourcesDir = "$buildDir/classes/java/main"
  }
  generated {
    java {
      srcDirs = ["$buildDir/generated-src"]
    }
  }
  test {
    // This is required for tests to "see" generated classes as well
    runtimeClasspath += generated.output
  }
}

task generateQuerydsl(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
  source = sourceSets.main.java
  classpath = configurations.compile + configurations.querydslApt
  options.compilerArgs = [
    '-proc:only',
    '-processor', 'com.querydsl.apt.jpa.JPAAnnotationProcessor'
  ]
  destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

compileJava {
  dependsOn generateQuerydsl
  source generateQuerydsl.destinationDir
}

compileGeneratedJava {
  dependsOn generateQuerydsl
  options.warnings = false
  classpath += sourceSets.main.runtimeClasspath
}
