// TODO: finish APT for querydsl
plugins {
  id 'nebula.provided-base' version '3.0.3'
  id 'java'
  id 'idea'
}

repositories {
  mavenCentral()
//    jcenter() for testng?
}

configurations {
  querydslapt
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
  querydslVersion = '4.1.4'
  hibernateVersion = '5.0.6.Final'
  h2Version = '1.4.190'
}

sourceSets {
  generated {
    java {
      srcDirs = ["$buildDir/generated-src"]
    }
  }
}

// this works, but right now it generates basic.iml which I ignore (using different name for module)
idea {
  module {
    generatedSourceDirs += sourceSets.generated.java.srcDirs
  }
}

dependencies {
  provided 'javax:javaee-api:7.0'
  compile "com.querydsl:querydsl-jpa:$querydslVersion"
  querydslapt "com.querydsl:querydsl-apt:$querydslVersion"
  compile "org.hibernate:hibernate-entitymanager:$hibernateVersion"
  compile "com.h2database:h2:$h2Version"

//    testCompile 'org.testng:testng:6.9.10'
}

task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
  source = sourceSets.main.java
  classpath = configurations.compile + configurations.querydslapt
  options.compilerArgs = [
    '-proc:only',
    '-processor', 'com.querydsl.apt.jpa.JPAAnnotationProcessor'
  ]
  destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

compileJava {
  dependsOn generateQueryDSL
  source generateQueryDSL.destinationDir
}

compileGeneratedJava {
  dependsOn generateQueryDSL
  options.warnings = false
  classpath += sourceSets.main.runtimeClasspath
}

test {
  useTestNG()
}

clean {
  delete sourceSets.generated.java.srcDirs
}